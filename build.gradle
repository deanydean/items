//
// Main build file for Items
plugins {
    id 'java'
    id 'maven-publish'
}

group = "org.oddcyb"

task run (type: JavaExec, dependsOn: classes){
    if(project.hasProperty('appargs')){
        args(appargs.split(','))
    }
    description = "Run Items Service"
    main = "items.Start"
    classpath = sourceSets.main.runtimeClasspath
}

jar {
    manifest {
        attributes(
                'Main-Class': "items.Start"
        )
    }

    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it).matching {
                include {
                    it.path.contains(".class") ||
                    it.path.contains(".properties")
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

publishing {
    publications {
        itemsLibrary(MavenPublication) {
            from components.java
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "com.google.code.gson:gson:2.8.2"
    implementation "com.sparkjava:spark-core:2.9.1"
    implementation "net.sf.jopt-simple:jopt-simple:6.0-alpha-3"
    implementation "com.mashape.unirest:unirest-java:1.4.9"
    implementation "org.mapdb:mapdb:3.0.5"
    implementation "org.mongodb:mongodb-driver:3.6.3"
    implementation "com.amazonaws:aws-java-sdk-dynamodb:1.11.+"
    implementation "org.slf4j:slf4j-simple:1.7.25"
    testImplementation "org.junit.jupiter:junit-jupiter:5.5.2"
}




